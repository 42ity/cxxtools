AC_INIT(cxxtools, 1.4.5, [Tommi Maekitalo <tommi@tntnet.org>])
AM_INIT_AUTOMAKE

AC_PREREQ([2.5.9])

abi_current=4
abi_revision=1
abi_age=0
sonumber=${abi_current}:${abi_revision}:${abi_age}
AC_SUBST(sonumber)

unset CDPATH

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([src/config.h])
AC_CONFIG_HEADERS([include/cxxtools/config.h])
AC_CONFIG_SRCDIR([src/base64stream.cpp])
AC_CONFIG_SUBDIRS(libltdl)

AC_HEADER_DIRENT

AC_PROG_CC
AC_PROG_CXX

AC_CHECK_LIB(nsl, setsockopt)
AC_CHECK_LIB(socket, accept)
AC_SUBST(NETWORK_LIBS)

AC_LANG(C++)
ACX_PTHREAD

AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_PROG_LIBTOOL
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)

AC_ARG_WITH(
   [logging],
   AS_HELP_STRING([--with-logging],
                  [specify logging-library. Accepted arguments:
                   log4cxx, log4cplus, cxxtools, disable or probe. Default is cxxtools]),
   [ ac_loggertype=$withval ],
   [ ac_loggertype=cxxtools ])

if test "$ac_loggertype" = "log4cxx" -o "$ac_loggertype" = "probe"; then
   AC_CHECK_HEADER(
      [log4cxx/logger.h],
      [ AC_DEFINE([CXXTOOLS_LOG_LOG4CXX],
                  [],
                  [use log4cxx for logging])
        ac_loggertype=log4cxx ],
      [ if test "$ac_loggertype" = "log4cxx"; then
          AC_MSG_ERROR(log4cxx not available)
        fi
      ]
   )
fi

if test "$ac_loggertype" = "log4cplus" -o "$ac_loggertype" = "probe"; then
   AC_CHECK_HEADER(
      [log4cplus/logger.h],
      [ AC_DEFINE([CXXTOOLS_LOG_LOG4CPLUS],
                  [],
                  [use log4cplus for logging])
        ac_loggertype=log4cplus ],
      [ if test "$ac_loggertype" = "log4cplus"; then
          AC_MSG_ERROR(log4cplus not available)
        fi
      ]
   )
fi

if test "$ac_loggertype" = "cxxtools" -o "$ac_loggertype" = "probe"; then
  AC_DEFINE([CXXTOOLS_LOG_LOGBUILTIN], [], [use cxxtools logging])
  ac_loggertype=cxxtools
elif test "$ac_loggertype" = "disable"; then
  AC_DEFINE([CXXTOOLS_LOG_DISABLE], [], [disable logging])
fi

case "$ac_loggertype" in
    log4cxx)
        AC_MSG_NOTICE([loggertype is log4cxx])
        CXXTOOLS_LOG_LDFLAGS=-llog4cxx
        CXXTOOLS_LOGGING=log4cxx
        ;;

    log4cplus)
        AC_MSG_NOTICE([loggertype is log4cplus])
        CXXTOOLS_LOG_LDFLAGS=-llog4cplus
        CXXTOOLS_LOGGING=log4cplus
        ;;

    cxxtools)
        AC_MSG_NOTICE([loggertype is cxxtools])
        CXXTOOLS_LOG_LDFLAGS=
        CXXTOOLS_LOGGING=cxxtools
        ;;

    disable)
        AC_MSG_NOTICE([logging is disabled])
        CXXTOOLS_LOGGING=disable
        ;;

    *)
        AC_MSG_ERROR([unknown loggertype $ac_loggertype])
        ;;
esac

CXXTOOLS_CXXFLAGS='-I${includedir}'
CXXTOOLS_LDFLAGS='-L${libdir} -lcxxtools'

AC_SUBST(CXXTOOLS_CXXFLAGS)
AC_SUBST(CXXTOOLS_LDFLAGS)
AC_SUBST(CXXTOOLS_LOG_LDFLAGS)
AC_SUBST(CXXTOOLS_LOGGING)

AC_ARG_WITH(
    [libtool],
    AS_HELP_STRING([--with-libtool],
                   [use libtool for dynamic loading of shared libraries (default: yes)]),
    ac_use_libtool=$withval,
    ac_use_libtool=yes,
    )

if test "$ac_use_libtool" = "yes"; then
AC_DEFINE([USE_LIBTOOL], [], [use libtool for dynamic loading of shared libraries])
fi

AC_CHECK_HEADER([iconv.h], , AC_MSG_ERROR([iconv not found]))
AC_COMPILE_IFELSE(
  [#include <iconv.h>
   void f(iconv_t cd)
   {
     const char* inbufptr = 0;
     size_t inbytesleft = 0;
     char* outbufptr = 0;
     size_t outbytesleft = 0;
     iconv(cd, &inbufptr, &inbytesleft, &outbufptr, &outbytesleft);
   }
  ],
  iconv_const=const
)

AC_DEFINE_UNQUOTED(ICONV_CONST, $iconv_const, [Define as const if the declaration of iconv() needs const.])

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  include/Makefile
  demo/Makefile
  cxxtools-config
  ])

AC_OUTPUT

chmod +x cxxtools-config
